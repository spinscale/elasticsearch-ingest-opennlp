buildscript {
  repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
 }

  dependencies {
    classpath "org.elasticsearch.gradle:build-tools:5.1.1"
    classpath "io.codearte.gradle.nexus:gradle-nexus-staging-plugin:0.5.3"
  }
}

plugins {
  id "de.undercouch.download" version "2.1.0"
}

import de.undercouch.gradle.tasks.download.Download

group = 'de.spinscale.elasticsearch.plugin.ingest'
version = '5.1.1.1'

// remove me again when removed from es core
// https://github.com/elastic/elasticsearch/pull/18955
if (GradleVersion.current() != GradleVersion.version('2.13')) {
  throw new GradleException('Gradle 2.13 is required to build elasticsearch')
}

apply plugin: 'java'
apply plugin: 'elasticsearch.esplugin'
apply plugin: 'idea'
apply plugin: 'maven'
apply plugin: 'signing'
apply plugin: 'io.codearte.nexus-staging'

esplugin {
  name 'ingest-opennlp'
  description 'Ingest processor that uses OpenNLP for named entity extraction'
  classname 'de.spinscale.elasticsearch.ingest.opennlp.IngestOpenNlpPlugin'
}

// In this section you declare the dependencies for your production and test code
// Elasticsearch dependency is included due to the build-tools, test-framework as well
dependencies {
  compile 'org.apache.opennlp:opennlp-tools:1.6.0'
}

bundlePlugin {
  from('src/test/resources/models') {
    into 'config/'
  }
}

integTest {
  cluster {
    setting 'ingest.opennlp.model.file.names',     'en-ner-persons.bin'
    setting 'ingest.opennlp.model.file.locations', 'en-ner-locations.bin'
    setting 'ingest.opennlp.model.file.dates',     'en-ner-dates.bin'
  }
}

// check style can be disabled, or you can configure a different checkstyle file
// checkstyleMain.enabled = false
// checkstyleTest.enabled = false

// FIXME dependency license check needs to be enabled
dependencyLicenses.enabled = false

// FIXME thirdparty audit needs to be enabled
thirdPartyAudit.enabled = false

// Uncomment this to skip license header checks
// licenseHeaders.enabled = false

// exlude the models for forbidden API check
forbiddenPatterns {
  exclude '**/*.bin'
}

// download the models before the tests are run, also needed for packaging
project.afterEvaluate {
  processTestResources.dependsOn downloadModels
}

// download the models but dont overwrite existing ones
task downloadModels << {
  downloadIfNotExists('http://opennlp.sourceforge.net/models-1.5/en-ner-person.bin',   'en-ner-persons.bin')
  downloadIfNotExists('http://opennlp.sourceforge.net/models-1.5/en-ner-location.bin', 'en-ner-locations.bin')
  downloadIfNotExists('http://opennlp.sourceforge.net/models-1.5/en-ner-date.bin',     'en-ner-dates.bin')
}

def downloadIfNotExists(String url, String file) {
  String dir = 'src' + File.separator + 'test' + File.separator + 'resources' + File.separator + 'models'
  new File(dir).mkdirs()
  if (new File(dir + File.separator + file).exists() == false) {
    download {
      src url
      dest new File(dir, file)
    }
  }
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

uploadArchives {
  repositories {
    mavenDeployer {
      beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

      repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
        authentication(userName: ossrhUsername, password: ossrhPassword)
      }

      snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
        authentication(userName: ossrhUsername, password: ossrhPassword)
      }

      pom.project {
        name 'Elasticsearch Ingest OpenNLP Detection Plugin'
        packaging 'zip'
        artifactId 'ingest-opennlp'
        description 'Ingest processor that uses OpenNLP for named entity extraction'
        url 'https://github.com/spinscale/elasticsearch-ingest-opennlp'

        scm {
          connection 'scm:git:https://github.com/spinscale/elasticsearch-ingest-opennlp'
          developerConnection 'scm:git:git@github.com:spinscale/elasticsearch-ingest-opennlp.git'
          url 'https://github.com/spinscale/elasticsearch-ingest-opennlp'
        }

        licenses {
          license {
            name 'The Apache License, Version 2.0'
            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
          }
        }

        developers {
          developer {
            id 'spinscale'
            name 'Alexander Reelsen'
            email 'alr@spinscale.de'
          }
        }
      }
    }
  }
}
